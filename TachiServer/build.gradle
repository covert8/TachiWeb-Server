plugins {
    id "application"
    id "com.jfrog.bintray" version "1.7.3"
    id 'maven-publish'
}

mainClassName = "xyz.nulldev.ts.TachiServer"

dependencies {
    // AndroidCompat
    compile project(":AndroidCompat")

    // Tachiyomi-App
    compile project(":Tachiyomi-App")

    // Tachiyomi web UI
    compile project(":tachiwebui")

    // Config API
    compile project(":TachiServer/Config")

    // Spark Web Framework
    compile "com.sparkjava:spark-core:2.5.5"

    // Javalin (new web framework)
    compile 'io.javalin:javalin:1.7.0'
    compile "com.fasterxml.jackson.core:jackson-databind:2.9.+"
    compile "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.+"

    // Vert.x (v3 web API)
    def vertx_version = '3.6.3'
    compile "io.vertx:vertx-web:$vertx_version"
    compile "io.vertx:vertx-web-api-contract:$vertx_version"
    compile "io.vertx:vertx-lang-kotlin:$vertx_version"
    compile "io.vertx:vertx-lang-kotlin-coroutines:$vertx_version"

    // Tests
    compile "junit:junit:4.12"

    // Database
    compile "org.xerial:sqlite-jdbc:3.16.1"

    // ICU Library (duplicated here from JVMPatch to fix resource bundle issues)
    compile group: 'com.ibm.icu', name: 'icu4j', version: '59.1'

    // Time extensions
    compile 'com.github.kizitonwose.time:time:1.0.1'

    // HTML builder
    compile "org.jetbrains.kotlinx:kotlinx-html-jvm:0.6.9"

    // Apache HTTP client
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.5'

    // Json path
    compile 'com.jayway.jsonpath:json-path:2.3.0'

    // Tika content-type detection
    def tika_version = "1.20"
    compile group: 'org.apache.tika', name: 'tika-core', version: tika_version
    compile "org.apache.tika:tika-parsers:$tika_version"
    compile group: 'org.apache.tika', name: 'tika-java7', version: tika_version
}

// Exclude regular ASM as dex2jar needs debug ASM
configurations.all {
    exclude group: "org.ow2.asm", module: "asm"
}

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

//Set application run dir
def runningDir = new File("build/app-run")
runningDir.mkdirs()
tasks.run.workingDir = runningDir.absolutePath

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['SnapshotPublication']
    publish = true
    pkg {
        repo = 'tachiweb'
        name = 'tachiweb-server-snapshots'
        version {
            name = "${getGitHash()}-DEV"
            desc = 'TachiWeb-Server development build'
            released  = new Date()
        }
    }
}

publishing {
    publications {
        SnapshotPublication(MavenPublication) {
            groupId 'xyz.nulldev.ts'
            artifactId 'tachiweb-server-snapshots'
            artifact(fatJar.archivePath) {
                builtBy assemble
            }
            version "${getGitHash()}-DEV"
        }
    }
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Version': version,
                'Main-Class': mainClassName
    }
    zip64 = true
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    exclude ('META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA')
    with jar
}

assemble.dependsOn fatJar
